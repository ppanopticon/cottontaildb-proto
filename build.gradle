group 'org.vitrivr'
version '0.9'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.8"
    }
}


apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.vitrivr'
            artifactId = 'cottontail-proto'
            version = System.getenv("MAVEN_PUBLICATION_VERSION")
            pom {
                name = 'Cottontail DB (gRPC)'
                description = 'The gRPC and ProtoBuf definitions for Cottontail DB.'
                url = 'https://github.com/vitrivr/cottontaildb-proto/'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'ppanopticon'
                        name = 'Ralph Gasser'
                        email = 'ralph.gasser@unibas.ch'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vitrivr/cottontaildb.git'
                    url = 'https://github.com/vitrivr/cottontaildb'
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/vitrivr/cottontaildb-proto"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

sourceSets {
    main {
        proto {
            srcDirs += "$projectDir/src/main/protobuf"
        }
        java {
            srcDirs += "$projectDir/build/generated/source/proto"
        }
    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/build/generated/source/proto"
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.29.0'
        }
    }
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.3'
    }
    generateProtoTasks {
        all()*.plugins { grpc {
            outputSubDir = 'java'
        }}
    }
}

dependencies {
    /////// gRPC & Protobuf
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.2'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.13.0'
    compile group: 'io.grpc', name: 'grpc-all', version: '1.33.1'
}